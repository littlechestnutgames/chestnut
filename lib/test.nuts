struct Test
    label : String
    expect : Any
    actual_fn : Function 
    skipped : Boolean
    iterations : Integer
endstruct

struct TestSuite
    name : String
    tests : List
    passed_tests : List
    skipped_tests : List
    failed_tests : List
    mixed_tests : List
endstruct

struct TestResult
    test : Test
    result : String
    mark : String
    start_time : Integer
    end_time : Integer
endstruct

fn new_test(label: String, expect : Any, actual_fn : Any, skip : Boolean = false, iterations : Integer = 1) returns Test
    let test = Test()
    test.label = label
    test.expect = expect
    test.actual_fn = actual_fn
    test.iterations = iterations

    return test
endfn

fn new_test_suite(suite_name : String)
    let test_suite = TestSuite()
    test_suite.name = suite_name
    test_suite.tests = []
    test_suite.passed_tests = []
    test_suite.skipped_tests = []
    test_suite.failed_tests = []
    test_suite.mixed_tests = []

    return test_suite
endfn

fn new_test_result(test : Test, result : String, mark : String, start_time : Integer, end_time : Integer)
    let test_result = TestResult()
    test_result.test = test
    test_result.result = result
    test_result.mark = mark
    test_result.start_time = start_time
    test_result.end_time = end_time

    return test_result
endfn

fn (ts : TestSuite) add_test_fn(label : String, expect : Any, actual_fn : Any, skipped : Boolean = false, iterations : Integer = 1)
    let test = new_test(label, expect, actual_fn, skipped, iterations)
    push(ts.tests, test)
endfn

fn (test : Test) run() returns TestResult
    let mark = "F"
    let res = null
    let start_time = null
    let end_time = null
    for range(0, test.iterations) as num
        start_time = get_time()

        let test_f = test.actual_fn
        res = null

        if not test.skipped
            res = test_f()
        endif

        if test.skipped
            mark = "-"
        elif test.expect == res
            mark = "."
        endif

        end_time = get_time()
    endfor
    print(mark)

    return new_test_result(test, res, mark, start_time, end_time)
endfn

fn (ts : TestSuite) run_tests()
    ts.passed_tests = []
    ts.skipped_tests = []
    ts.failed_tests = []
    ts.mixed_tests = []
    for ts.tests as test
        let test_result = test.run()
        case test_result.mark
        when "."
            push(ts.passed_tests, test_result)
            push(ts.mixed_tests, test_result)
        when "-"
            push(ts.skipped_tests, test_result)
            push(ts.mixed_tests, test_result)
        when "F"
            push(ts.failed_tests, test_result)
            push(ts.mixed_tests, test_result)
        otherwise
            halt("Unknown report result \"{{ test_result }}\"")
        endcase
    endfor
endfn

fn (tr : TestResult) display_test_result()
    printline("* \"{{ tr.test.label }}\" - took {{ round(tr.end_time - tr.start_time, 4) }}s")
endfn

fn (ts : TestSuite) display_simple()
    if length(ts.tests) > 0
        if length(ts.passed_tests) + length(ts.skipped_tests) + length(ts.failed_tests) == 0
            ts.run_tests()
        endif
        for ts.mixed_tests as test_result
            print(test_result.mark)
        endfor
        print("\n")
    endif
endfn

import "string" (repeat)

fn make_header(str : String)
    let slen = int(ceil(length(str) / 2))
    let left_len = 40 - slen - 2
    let right_len = 40 - slen - 1

    return "{{ repeat("#", left_len) }} {{ str }} {{ repeat("#", right_len) }}"
endfn

fn (ts : TestSuite) display_results(show_skipped : Boolean = true, show_passed : Boolean = true)
    let header = ""
    if length(ts.name) >= 78
        header = "###############################################################################\n{{ ts.name }}\n###############################################################################\n"
    else
        header = make_header(ts.name)
    endif
    printline(header)

    let footer = "###############################################################################\n"

    print("Results: ")
    let left_equals = length(ts.name)
    if length(ts.tests) == 0
        printline("No tests to run!")
        return null
    elif length(ts.passed_tests) + length(ts.skipped_tests) + length(ts.failed_tests) == 0
        # Run the tests if they've not been run yet.
        ts.run_tests()
    endif
    printline("\n")

    if show_passed
        printline("Passed: {{ length(ts.passed_tests) }}")
        for ts.passed_tests as passed_test_result
            passed_test_result.display_test_result()
        endfor
        print("\n")
    endif
    if show_skipped
        printline("Skipped: {{ length(ts.skipped_tests) }}")
        for ts.skipped_tests as skipped_test_result
            skipped_test_result.display_test_result()
        endfor
        print("\n")
    endif
    printline("Failed : {{ length(ts.failed_tests) }}")
    for ts.failed_tests as failed_test_result
        failed_test_result.display_test_result()
    endfor
    print("\n")
    printline(footer)
endfn

fn assert_test(label : String, expect : Any, actual_fn : Any)
    print("\nLabel: {{ label }}\n===============================================================================\nExpect: {{ expect }}\nactual_fn: {{ actual_fn }}\n")
    assert(expect, actual_fn)
endfn


